plugins {
    id 'java'
    id 'maven-publish'
    id 'kr.entree.spigradle' version '2.2.3'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = "io.github.plugintemplate"
version = "1.0"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    jcenter()
    spigot()
    jitpack()
    maven {
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
    maven {
        url = "https://dl.bintray.com/portlek/maven"
    }
    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url = "https://repo.codemc.org/repository/maven-public"
    }
}

dependencies {
    // Tools
    implementation("co.aikar:acf-bukkit:0.5.0-SNAPSHOT")
    implementation("io.github.portlek:configs-core:4.9.9.6")
    implementation("io.github.portlek:configs-bukkit:5.15.2")
    implementation("io.github.portlek:configs-yaml:4.9.9.6")
    implementation("io.github.portlek:configs-json:4.9.9.6")
    implementation("io.github.portlek:SmartInventory:4.1.1")
    implementation("io.github.portlek:BukkitItemBuilder:4.0.1")
    implementation("io.github.portlek:bukkit-location:2.0.10")
    implementation("io.github.bananapuncher714:NBTEditor:7.16")
    implementation("co.aikar:idb-bukkit:1.0.0-SNAPSHOT")
    implementation("com.zaxxer:HikariCP:4.0.3")

    // Other Stuffs
    compileOnly("org.jetbrains:annotations:20.1.0")
    compileOnly("org.projectlombok:lombok:1.18.20")
    annotationProcessor("org.projectlombok:lombok:1.18.20")

    // Main
    compileOnly spigot()

    // Hooks
    compileOnly("me.clip:placeholderapi:2.10.6")
    compileOnly(vault()) {
        exclude group: 'org.bukkit'
    }
    compileOnly luckPerms()
}

shadowJar {
    // Change the "bukkittemplate" case.
    relocate('co.aikar.commands', this.group + ".bukkittemplate.commands")
    relocate('io.github.portlek.configs', this.group + ".bukkittemplate.file")
    relocate('org.simpleyaml', this.group + ".bukkittemplate.simpleyaml")
    relocate('co.aikar.idb', this.group + ".bukkittemplate.db")
    minimize()
    setArchivesBaseName(this.name)
    archiveClassifier.set("")
    archiveVersion.set("")
}

spigot {
    description = "Plugin Template for Bukkit on Java with Gradle."
    apiVersion = "1.13"
    authors = ["portlek"]
    softDepends = ["Vault", "LuckPerms", "PlaceholderAPI"]
    commands {
        bukkittemplate {
            description = 'Main command of the plugin.'
            permission = 'bukkittemplate.command.main'
            permissionMessage = 'You do not have permission!'
            usage = '/<command> [help|reload|version]'
            aliases = ['bt']
        }
    }
    permissions {
        'bukkittemplate.*' {
            description = 'Allows you to access to the all plugin.'
            defaults = 'op'
            children = [
                'bukkittemplate.command.*': true
            ]
        }
        'bukkittemplate.command.*' {
            description = 'It allows you to use to all commands of the plugin.'
            defaults = 'op'
            children = [
                'bukkittemplate.command.help'   : true,
                'bukkittemplate.command.reload' : true,
                'bukkittemplate.command.version': true,
                'bukkittemplate.command.message': true
            ]
        }
        'bukkittemplate.version' {
            description = 'It allows you to notify if there is a new update for the plugin on join.'
            defaults = 'op'
        }
        'bukkittemplate.command.main' {
            description = 'It allows you to use `/bukkittemplate` command.'
            defaults = 'true'
        }
        'bukkittemplate.command.help' {
            description = 'It allows you to use to `/bukkittemplate help` command.'
            defaults = 'true'
        }
        'bukkittemplate.command.reload' {
            description = 'It allows you to use to `/bukkittemplate reload` command.'
            defaults = 'op'
        }
        'bukkittemplate.command.version' {
            description = 'It allows you to use to `/bukkittemplate version` command.'
            defaults = 'op'
        }
        'bukkittemplate.command.message' {
            description = 'It allows you to use to `/bukkittemplate message` command.'
            defaults = 'true'
        }
    }
}
